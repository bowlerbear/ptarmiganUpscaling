
    model{
    
    #################
    #Detection model#
    #################
    
    pi <- 3.141593
    
    # priors for fixed effect parms for half-normal detection parm sigma
    b.df.0 ~ dunif(0,20)        
    b.group.size ~ dnorm(0,0.01)
    
    #adm random effect on detection
    for(i in 1:nadm){
      b.adm[i] ~ dnorm(0,b.adm.tau)
    }
    b.adm.sd ~ dt(0, pow(2.5,-2), 1)T(0,)
    b.adm.tau <- pow(b.adm.sd,-2)
    
    for(i in 1:ndetections){
    #linear predictor
    mu.df[i] <- b.df.0 + b.group.size * ln_GroupSize[i] + b.adm[detectionAdm[i]]
    
    # estimate of sd and var, given coeffs above
    sig.df[i] <- exp(mu.df[i])
    sig2.df[i] <- sig.df[i]*sig.df[i]
    
    # effective strip width
    esw[i] <- sqrt(pi * sig2.df[i] / 2) 
    f0[i] <- 1/esw[i] #assuming all detected on the line
    
    # LIKELIHOOD
    # using zeros trick
    L.f0[i] <- exp(-y[i]*y[i] / (2*sig2.df[i])) * 1/esw[i] #y are the distances
    nlogL.f0[i] <-  -log(L.f0[i])
    zeros.dist[i] ~ dpois(nlogL.f0[i])
    }

    #using this model and predicted group size (below), get predicted ESW 
    for(t in 1:nyear){
      for(j in 1:nsite){
        pred.sig[j,t] <- exp(b.df.0 + b.group.size * log(GroupSizes[j,t]+1) + b.adm[adm[j]]) 
        pred.sig2[j,t] <- pow(pred.sig[j,t],2)
        predESW[j,t] <- sqrt(pi * pred.sig2[j,t] / 2)
      }
    }

    ########################
    #Model of total density#
    ########################

    #intercept
    int.d ~ dnorm(0,0.001)    
    
    #random line effect
    line.d.sd ~ dt(0, pow(2.5,-2), 1)T(0,)
    line.d.tau <- pow(line.d.sd,-2)
    for(j in 1:nsite){
      random.d.line[j] ~ dnorm(0,line.d.tau)
    }
    
    #random adm effect
    adm.d.sd ~ dt(0, pow(2.5,-2), 1)T(0,)
    adm.d.tau <- pow(adm.d.sd,-2)
    for(j in 1:nadm){
      random.d.adm[j] ~ dnorm(0,adm.d.tau)
    }
    
    #random year effect
    year.d.sd ~ dt(0, pow(2.5,-2), 1)T(0,)
    year.d.tau <- pow(year.d.sd,-2)
    for(t in 1:nyear){
      random.d.year[t] ~ dnorm(0,year.d.tau)
    }

    #covariate effects - L1 regularization == a Laplace (double exponential) prior 
    #https://stats.stackexchange.com/questions/28609/regularized-bayesian-logistic-regression-in-jags
    for(i in 1:n.covs){
      beta[i] ~ ddexp(0, dd.tau)
    }
    dd.tau ~ dgamma(0.01, 0.01)

    #Observation model:
    r ~ dunif(0,50)
    for(j in 1:nsite){
      for(t in 1:nyear){
        
        NuIndivs[j,t] ~ dnegbin(p[j,t],r)
        
        p[j,t] <- r/(r+expNuIndivs[j,t])
        
        #NuIndivs[j,t] ~ dpois(expNuIndivs[j,t])
        
        expNuIndivs[j,t] <- Density.jt[j,t] * TransectLength[j,t]/1000 * predESW[j,t]/1000 * 2
        
      }
    }
    
    #State model
    for(j in 1:nsite){
      for(t in 1:nyear){
      
      #linear predictor on density
        log(Density.jt[j,t]) <- int.d + 
                            random.d.year[t] +
                            inprod(beta[],occDM[j,]) +
                            random.d.adm[adm[j]] 
                            #random.d.line[j] 
                            
                            

    }}
    
    #predict observations - original scale and density scale
    for(j in 1:nsite){
      #original scale
      meanExpNu[j] <- mean(expNuIndivs[j,])
      #density scale 
      meanDensity[j] <- mean(Density.jt[j,])
      
    }
    
  
    #mean density at grids across Norway
    for(j in 1:n.preds){
      log(Density.km[j]) <- int.d + inprod(beta[],predDM[j,]) + random.d.adm[pred.adm[j]]  
      Density[j] <- Density.km[j]*25 # since there are 25 km per grid
    }

    #totalPop <- sum(Density)

    #calculate the Bayesian p-value
    e <- 0.0001
    
    for(j in 1:nsite){
      for(t in 1:nyear){
        
        #expected value of model
        exp[j,t] <- (r*(1-p[j,t]))/p[j,t]
        
        #compared to data
        chi2[j,t] <- pow((NuIndivs[j,t] - exp[j,t]),2) / (sqrt(exp[j,t])+e)
        
        #New samples
        #compared to simulated values
        NuIndivs.new[j,t] ~ dnegbin(p[j,t],r)
        
        chi2.new[j,t] <- pow((NuIndivs.new[j,t] - exp[j,t]),2) / (sqrt(exp[j,t])+e) 
        
      }
    }
    

    # Add up discrepancy measures for entire data set
    for(t in 1:nyear){
      fit.t[t] <- sum(chi2[,t])                     
      fit.new.t[t] <- sum(chi2.new[,t])             
    }
    
    fit <- sum(fit.t[])
    fit.new <- sum(fit.new.t[])
    
    }
    
