
    model{

    #################
    #Detection model#
    #################
    
    pi <- 3.141593
    
    # priors for fixed effect parms for half-normal detection parm sigma
    b.df.0 ~ dunif(0,20)        
    b.group.size ~ dnorm(0,0.001)

    for(i in 1:ndetections){
    #linear predictor
    mu.df[i] <- b.df.0 + b.group.size * log.detectionGroupSize[i] 
    
    # estimate of sd and var, given coeffs above
    sig.df[i] <- exp(mu.df[i])
    sig2.df[i] <- sig.df[i]*sig.df[i]
    
    # effective strip width
    esw[i] <- sqrt(pi * sig2.df[i] / 2) 
    f0[i] <- 1/esw[i] #assuming all detected on the line
    
    # LIKELIHOOD
    # using zeros trick
    #y[i] ~ dunif(0,W) 
    L.f0[i] <- exp(-y[i]*y[i] / (2*sig2.df[i])) * 1/esw[i] #y are the distances
    nlogL.f0[i] <-  -log(L.f0[i])
    zeros.dist[i] ~ dpois(nlogL.f0[i])
    }

    #using this model and predicted group size (below), get predicted ESW 
    for(t in 1:nyear){
      for(j in 1:nsite){
        pred.sig[j,t] <- exp(b.df.0 + b.group.size * log(predGroupSize[j,t]+1)) 
        pred.sig2[j,t] <- pow(pred.sig[j,t],2)
        predESW[j,t] <- sqrt(pi * pred.sig2[j,t] / 2)
      }
    }

    ##################
    #Group size model#
    ##################

    #priors
    int.gs ~ dnorm(0,0.001)    
    
    #random line effect
    line.sd ~ dunif(0,10)
    line.tau <- pow(line.sd,-2)
    for(j in 1:nsite){
      random.gs.line[j] ~ dnorm(0,line.tau)
    }
    
    #random time effect
    year.sd ~ dunif(0,10)
    year.tau <- pow(year.sd,-2)
    for(t in 1:nyear){
      random.gs.year[t] ~ dnorm(0,year.tau)
    }

    #Model
    #for each detection, model group size
    for(i in 1:ndetections){
      GroupSize[i] ~ dpois(expGroupSize[i])
      log(expGroupSize[i]) <- int.gs + random.gs.year[detectionYear[i]] + 
                                      random.gs.line[detectionSite[i]] 
    }

    #using this model, get predicted group size for each line and year
    for(t in 1:nyear){
      for(j in 1:nsite){
        log(predGroupSize[j,t]) <- int.gs + random.gs.year[t] + random.gs.line[j] 
      }
    }

    ########################
    #Model of total density#
    ########################

    #intercept
    int.d ~ dnorm(0,0.001)    
    
    #random line effect
    line.d.sd ~ dunif(0,10)
    line.d.tau <- pow(line.d.sd,-2)
    for(j in 1:nsite){
      random.d.line[j] ~ dnorm(0,line.d.tau)
    }
    
    #random year effect
    year.d.sd ~ dunif(0,10)
    year.d.tau <- pow(year.d.sd,-2)
    for(t in 1:nyear){
      random.d.year[t] ~ dnorm(0,year.d.tau)
    }

    #covariate effects
    for(i in 1:n.covs){
      beta[i] ~ dnorm(0,0.1)
    }

    #Observation model:
    for(j in 1:nsite){
      for(t in 1:nyear){
        NuIndivs[j,t] ~ dpois(expNuIndivs[j,t])
        expNuIndivs[j,t] <- (predDensity[j,t] * (TransectLength[j,t]/1000 * predESW[j,t]/1000 * 2))
        predDensity[j,t] ~ dpois(Density[j,t])  
      }}

    #State model
    for(j in 1:nsite){
      for(t in 1:nyear){
      
      #linear predictor on density
        log(Density[j,t]) <- int.d + 
                            random.d.line[j] + 
                            random.d.year[t] +
                            inprod(beta[],occDM[j,]) 

    }}


    #calculate the Bayesian p-value
    e <- 0.0001
    for(j in 1:nsite){
      for(t in 1:nyear){
        chi2[j,t] <- pow((NuIndivs[j,t] - expNuIndivs[j,t]),2) / (sqrt(expNuIndivs[j,t])+e)
        expNuIndivs.new[j,t] ~ dpois(expNuIndivs[j,t]) 
        chi2.new[j,t] <- pow((expNuIndivs.new[j,t] - expNuIndivs[j,t]),2) / (sqrt(expNuIndivs[j,t])+e) # exp
      }
    }
    
    # Add up discrepancy measures for entire data set
    for(t in 1:nyear){
      fit.t[t] <- sum(chi2[,t])                     
      fit.new.t[t] <- sum(chi2.new[,t])             
    }
    fit <- sum(fit.t[])
    fit.new <- sum(fit.new.t[])
    bpv <- step(fit.new - fit)    # Bayesian p-value
    
    }
    
