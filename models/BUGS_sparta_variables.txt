
  model{
  # JAGS code for SPARTA model plus random walk prior
  # on the year effect of the state model + intercept + halfcauchy hyperpriors
  
  # State model
  for (i in 1:nsite){ 
    for (t in 1:nyear){
      z[i,t] ~ dbern(muZ[i,t]) 
      logit(muZ[i,t])<- a[t] + eta[i] + habitat[i]*beta.habitat + bio1[i]*beta.bio1 + bio6[i]*beta.bio6
      # year as a fixed factor and site as a random factor and environ variables
    }
  }   
  
  
  ### Observation Model
  for(j in 1:nvisit) {
    y[j] ~ dbern(Py[j]) #data is Y
    Py[j]<- z[site[j],year[j]]*p[j] #probability to detect = prob of occ * prob of detection

    #detection model:
    logit(p[j]) <-  alpha.p[year[j]] + dtype.p*L[j] 
    #depends on year and list length
    } 
  
  # Derived parameters
  for (t in 1:nyear) {  
    psi.fs[t] <- sum(z[1:nsite, t])/nsite
  } 

  #Priors 

  # State model priors
    #year effects
    #year 1
    a[1] ~ dnorm(0, 0.001)

    #other years
    for(t in 2:nyear){
      a[t] ~ dnorm(a[t-1], tau.a)
    }
    tau.a <- 1/(sd.a * sd.a)
    sd.a ~ dt(0, 1, 1)T(0,) 
    
    #site effects
    for (i in 1:nsite) {
      eta[i] ~ dnorm(0, tau2)       
    } 
    tau2 <- 1/(sigma2 * sigma2) 
    sigma2 ~ dt(0, 1, 1)T(0,) 


    #Observation model priors 
    #year effects
    for (t in 1:nyear) {
      alpha.p[t] ~ dnorm(mu.lp, tau.lp)            
    }
    
    mu.lp ~ dnorm(0, 0.01)
    tau.lp <- 1 / (sd.lp * sd.lp)                 
    sd.lp ~ dt(0, 1, 1)T(0,)  
    
    #ecological covariates
    beta.habitat ~ dnorm(0,0.01)
    beta.bio1 ~ dnorm(0,0.01)
    beta.bio6 ~ dnorm(0,0.01)

    #observation model covariates
    dtype.p ~ dnorm(0, 0.01)

  }
    
