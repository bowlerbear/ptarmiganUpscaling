
  model{
  # JAGS code for SPARTA model plus random walk prior
  # on the year effect of the state model + intercept + halfcauchy hyperpriors
  
  # State model
  for (i in 1:nsite){ 
    for (t in 1:nyear){
      z[i,t] ~ dbern(muZ[i,t]) 
      logit(muZ[i,t])<- eta[i] + a[t]
      # year as a fixed factor and site as a random factor and environ variables
    }
  }   
  
  eta <- X %*% b ## linear predictor

  ### Observation Model
  for(j in 1:nvisit) {
    y[j] ~ dbern(Py[j]) #data is Y
    Py[j]<- z[site[j],year[j]]*p[j] #probability to detect = prob of occ * prob of detection

    #detection model:
    logit(p[j]) <-  alpha.p[year[j]] + dtype.p*L[j] 
    #depends on year and list length
    } 
  
  # Derived parameters
  for (t in 1:nyear) {  
    psi.fs[t] <- sum(z[1:nsite, t])/nsite
  } 

  #Priors 

  ## Parametric effect priors CHECK tau=1/0.61^2 is appropriate!
  for (i in 1:1) { b[i] ~ dnorm(0,2.7) }
  ## prior for s(x,y)... 
  K1 <- S1[1:9,1:9] * lambda[1]  + S1[1:9,10:18] * lambda[2]
  b[2:10] ~ dmnorm(zero[2:10],K1) 
  ## smoothing parameter priors CHECK...
  for (i in 1:2) {
  rho[i] ~ dunif(-12,12)
  lambda[i] <- exp(rho[i])
  }

  #random site effect
  for(i in 1:nyear){
    a[i] ~ dnorm(0,tau.a)
  }
  tau.a <- pow(sd.a, -2)
  sd.a ~ dunif(0,10)

  #Observation model priors 
  #year effects
  for (t in 1:nyear) {
      alpha.p[t] ~ dnorm(mu.lp, tau.lp)            
  }
    
  mu.lp ~ dnorm(0, 0.01)
  tau.lp <- 1 / (sd.lp * sd.lp)                 
  sd.lp ~ dt(0, 1, 1)T(0,)  
    

  #observation model covariates
  dtype.p ~ dnorm(0, 0.01)

  }
    
