
    model{
    
    #################
    #Detection model#
    #################
    
    pi <- 3.141593
    
    # priors for fixed effect parms for half-normal detection parm sigma
    b.df.0 ~ dunif(0,20)        
    b.group.size ~ dnorm(0,0.01)
    
    #adm random effect on detection
    for(i in 1:nadm){
      b.adm[i] ~ dnorm(0,b.adm.tau)
    }
    b.adm.sd ~ dt(0, pow(2.5,-2), 1)T(0,)
    b.adm.tau <- pow(b.adm.sd,-2)
    
    for(i in 1:ndetections){
    #linear predictor
    mu.df[i] <- b.df.0 + b.group.size * ln_GroupSize[i] + b.adm[detectionAdm[i]]
    
    # estimate of sd and var, given coeffs above
    sig.df[i] <- exp(mu.df[i])
    sig2.df[i] <- sig.df[i]*sig.df[i]
    
    # effective strip width
    esw[i] <- sqrt(pi * sig2.df[i] / 2) 
    f0[i] <- 1/esw[i] #assuming all detected on the line
    
    # LIKELIHOOD
    # using zeros trick
    L.f0[i] <- exp(-y[i]*y[i] / (2*sig2.df[i])) * 1/esw[i] #y are the distances
    nlogL.f0[i] <-  -log(L.f0[i])
    zeros.dist[i] ~ dpois(nlogL.f0[i])
    }

    #using this model and predicted group size (below), get predicted ESW 
    for(t in 1:nyear){
      for(j in 1:nsite){
        pred.sig[j,t] <- exp(b.df.0 + b.group.size * log(GroupSizes[j,t]+1) + b.adm[adm[j]]) 
        pred.sig2[j,t] <- pow(pred.sig[j,t],2)
        predESW[j,t] <- sqrt(pi * pred.sig2[j,t] / 2)
      }
    }

    ########################
    #Model of total density#
    ########################

    #intercept
    int.d ~ dnorm(0,0.001)    
    
    #Observation model:
    r ~ dunif(0,50)
    for(j in 1:nsite){
      for(t in 1:nyear){
        
        NuIndivs[j,t] ~ dnegbin(p[j,t],r)
        
        p[j,t] <- r/(r+expNuIndivs[j,t])
        
        #NuIndivs[j,t] ~ dpois(expNuIndivs[j,t])
        
        expNuIndivs[j,t] <- Density.jt[j,t] * TransectLength[j,t]/1000 * predESW[j,t]/1000 * 2
        
      }
    }
    
    #State model
    for(j in 1:nsite){
      for(t in 1:nyear){
      
      #linear predictor on density
        log(Density.jt[j,t]) <- int.d 
                            
    }}
    
    
    #predict observations - original scale and density scale
    for(j in 1:nsite){
      meanDensity[j] <- mean(Density.jt[j,])
      mid.expNuIndivs[j] <- expNuIndivs[j,6]
      
    }
  
    #mean density at grids across Norway
    for(j in 1:n.preds){
      for(t in 1:nyear){
      
      #for each grid
      log(Density.km.pt[j,t]) <- int.d 
      
      Density.pt[j,t] <- Density.km.pt[j,t]*25
                            
      }
      
      Density.km.p[j] <- mean(Density.km.pt[j,])#mean per year
      Density.p[j] <- Density.km.p[j]*25 # since there are 25 km per grid
    }

    #totalPop <- sum(Density)

    #calculate the Bayesian p-value - at the site-level
    
    #get expected values per site
    for(j in 1:nsite){
      for(t in 1:nyear){ 

        #expected value of model
        exp[j,t] <- (r*(1-p[j,t]))/p[j,t]
        #new samples
        NuIndivs.new[j,t] ~ dnegbin(p[j,t],r)
        
      }
      exp.j[j] <- mean(exp[j,])
      NuIndivs.j[j] <- mean(NuIndivs[j,])
      NuIndivs.new.j[j] <- mean(NuIndivs.new[j,])
    }
    
    e <- 0.0001
    
    #do discrepancy at the site-level
    for(j in 1:nsite){
        
        #compared to data
        chi2[j] <- pow((NuIndivs.j[j] - exp.j[j]),2) / (sqrt(exp.j[j])+e)
        
        #compared to simulated values
        chi2.new[j] <- pow((NuIndivs.new.j[j] - exp.j[j]),2) / (sqrt(exp.j[j])+e) 
      
    }
    
    # Add up discrepancy measures for entire data set
      fit <- sum(chi2[])                     
      fit.new <- sum(chi2.new[])    
      
      test <- step(fit.new-fit) 	
      bpv <- mean(test)
    
    }
    
